apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def releasePropertiesFile = rootProject.file("private/release.properties")
def releaseProperties = new Properties()
releaseProperties.load(new FileInputStream(releasePropertiesFile))

def currentCommit = 'git rev-parse --short HEAD'.execute().text.trim().subSequence(0, 7)

android {
  compileSdkVersion 28
  defaultConfig {
    applicationId "com.ahmednts.scylla"
    minSdkVersion 21
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
  }

  signingConfigs {
    debugConfig {
      storeFile rootProject.file("private/debug.keystore")
      storePassword 'android'
      keyAlias 'androiddebugkey'
      keyPassword 'android'
    }
    releaseConfig {
      storeFile rootProject.file("private/release.keystore")
      storePassword releaseProperties['storePassword']
      keyAlias releaseProperties['keyAlias']
      keyPassword releaseProperties['keyPassword']
    }
  }

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      debuggable true
      applicationIdSuffix '.debug'
      versionNameSuffix "_${currentCommit}"

      signingConfig signingConfigs.debugConfig
    }
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

      signingConfig signingConfigs.releaseConfig
    }
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  lintOptions {
    // Don't abort if Lint finds an error, otherwise the Jenkins build
    // will be marked as failed, and Jenkins won't analyse the Lint output
    abortOnError false
    warning 'InvalidPackage'
  }

  applicationVariants.all { variant ->
    variant.outputs.all {
      outputFileName =
          outputFileName.replace(".apk", "-${variant.versionCode}-${variant.versionName}.apk")
    }
  }
}

repositories {
  mavenCentral()
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

  implementation "com.android.support:support-v4:$support_version"
  implementation "com.android.support:appcompat-v7:$support_version"
  implementation "com.android.support:recyclerview-v7:$support_version"
  implementation 'com.android.support.constraint:constraint-layout:1.1.3'
  implementation "com.android.support:design:$support_version"
  implementation "com.android.support:cardview-v7:$support_version"
  implementation "com.android.support:support-vector-drawable:$support_version"

  //arch components
  implementation "android.arch.lifecycle:extensions:$arch_version"
  kapt "android.arch.lifecycle:compiler:$arch_version"

  //RxJava & RxAndroid
  implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
  implementation 'io.reactivex.rxjava2:rxjava:2.2.2'

  //Networking
  implementation "com.squareup.okhttp3:okhttp:$okhttp_version"
  implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"
  implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
  implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"
  implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
  implementation "com.google.code.gson:gson:$gson_version"

  //Utils
  implementation("com.github.bumptech.glide:glide:$glide_version") {
    exclude group: "com.android.support"
  }
  kapt "com.github.bumptech.glide:compiler:$glide_version"
  implementation 'jp.wasabeef:glide-transformations:4.0.0'

  implementation 'com.jakewharton.timber:timber:4.5.1'

  //  debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
  //  releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"
  //  testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakcanary_version"

  testImplementation 'junit:junit:4.12'
}

configurations.all {
  resolutionStrategy {
    force "com.android.support:support-v4:$support_version"
    force "com.android.support:appcompat-v7:$support_version"
    force "com.android.support:design:$support_version"
    force "com.android.support:cardview-v7:$support_version"
    force "com.android.support:customtabs:$support_version"
  }
}

// Logger out test results to console
tasks.matching { it instanceof Test }.all {
  testLogging.events = ["failed", "passed", "skipped"]
}
